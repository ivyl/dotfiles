#!/usr/bin/env zsh

set -ue

function sanitize_password_store_dir {
    PASSWORD_STORE_DIR=${PASSWORD_STORE_DIR-~/.password-store}

    if [[ ! -d "$PASSWORD_STORE_DIR" ]]; then
        printf "Configure \$PASSWORD_STORE_DIR.\n" 1>&2
        exit 1
    fi
}


function dmenu_select {
    local matches=("$PASSWORD_STORE_DIR"/**/*.gpg)
    local matches=("${matches[@]#"$PASSWORD_STORE_DIR"/}")
    local matches=("${matches[@]%.gpg}")

    local choice="$(printf '%s\n' ${matches[@]} | dmenu -p 'pass-autotype' $@)"

    if [[ -z $choice ]]; then
        printf "Nothing choosen.\n" 1>&2
        exit 1
    fi

    echo "$choice"
}


function xdotool_type_choice {
    local choice="$1"

    pass "$choice" |
        { read -r pass; printf %s "$pass"; } |
        xdotool type --clearmodifiers --file -
}


function main {
    sanitize_password_store_dir

    local choice=$(dmenu_select $@)
    xdotool_type_choice $choice
}


main $@
